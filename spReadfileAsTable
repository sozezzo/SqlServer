/*
*  Name : #spReadfileAsTable
*  Description : Read file as table
*
*  Syntax :
*    DECLARE @file TABLE ( [LineNo] int, line varchar(MAX));
*    INSERT INTO @file ([LineNo], [line]) EXEC #spReadfileAsTable @Path = 'C:\TEMP', @filename = 'Test.csv'
*    SELECT [LineNo], [line] FROM @file;*
*/
CREATE OR ALTER PROCEDURE #spReadfileAsTable
( @Path VARCHAR(255)    
, @Filename VARCHAR(100)
)
AS
BEGIN

    -- https://www.red-gate.com/simple-talk/sql/t-sql-programming/reading-and-writing-files-in-sql-server-using-t-sql/
    -- https://www.red-gate.com/simple-talk/wp-content/uploads/2017/07/uftReadFileAsTable.txt

	SET NOCOUNT ON;

	DECLARE @fileStr NVARCHAR(MAX) = '';
	DECLARE @file TABLE ( [LineNo] int IDENTITY(1,1)
	,                     line     varchar(MAX) )

	DECLARE @objFileSystem int , @objTextStream int , @objErrorObject int , @strErrorMessage Varchar(1000) , @Command varchar(1000) , @hr int , @String VARCHAR(8000) , @YesOrNo INT

	SELECT @strErrorMessage='opening the File System Object'
	EXECUTE @hr = sp_OACreate 'Scripting.FileSystemObject'
	,                         @objFileSystem OUT


	IF @HR=0
		SELECT @objErrorObject=@objFileSystem
		,      @strErrorMessage='Opening file "'+@path+'\'+@filename+'"'
		,      @command=@path+'\'+@filename

	IF @HR=0
		EXECUTE @hr = sp_OAMethod @objFileSystem
		,                         'OpenTextFile'
		,                         @objTextStream OUT
		,                         @command
		,                         1
		,                         false
		,                         0--for reading, FormatASCII

	WHILE @hr=0
	BEGIN
		IF @HR=0
			SELECT @objErrorObject=@objTextStream
			,      @strErrorMessage='finding out if there is more to read in "'+@filename+'"'
		IF @HR=0
			EXECUTE @hr = sp_OAGetProperty @objTextStream
			,                              'AtEndOfStream'
			,                              @YesOrNo OUTPUT

		IF @YesOrNo<>0
			BREAK

		IF @HR=0
			SELECT @objErrorObject=@objTextStream
			,      @strErrorMessage='reading from the output file "'+@filename+'"'
		IF @HR=0
			EXECUTE @hr = sp_OAMethod @objTextStream
			,                         'Readline'
			,                         @String OUTPUT
		INSERT INTO @file ( line )
		SELECT @String
	END

	IF @HR=0
		SELECT @objErrorObject=@objTextStream
		,      @strErrorMessage='closing the output file "'+@filename+'"'
	IF @HR=0
		EXECUTE @hr = sp_OAMethod @objTextStream
		,                         'Close'


	IF @hr<>0
	BEGIN

		DECLARE @Source varchar(255) , @Description Varchar(255) , @Helpfile Varchar(255) , @HelpID int

		EXECUTE sp_OAGetErrorInfo @objErrorObject
		,                         @source OUTPUT
		,                         @Description OUTPUT
		,                         @Helpfile OUTPUT
		,                         @HelpID OUTPUT;

		SELECT @strErrorMessage='Error whilst '
		+COALESCE(@strErrorMessage,'doing something')
		+', '+COALESCE(@Description,'')
	
		INSERT INTO @File ( line ) SELECT @strErrorMessage;

	END

	EXECUTE sp_OADestroy @objTextStream
	-- Fill the table variable with the rows for your result set

	SELECT [LineNo], line FROM @file

	RETURN

END
GO

GO
DECLARE @file TABLE ( [LineNo] int, line varchar(MAX));
INSERT INTO @file ([LineNo], [line]) EXEC #spReadfileAsTable @Path = 'C:\TEMP', @filename = 'Test.csv'
SELECT [LineNo], [line] FROM @file;
